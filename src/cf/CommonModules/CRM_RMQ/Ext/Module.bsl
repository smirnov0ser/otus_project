
Процедура РегламентноеЗадание_ОтправитьВRMQ() Экспорт
	//1. Новые заказы 
	Маршрут = "CRM_NewOrder";  
	ПараметрыRMQ = РегистрыСведений.CRM_ПараметрыПодключенияRMQ.ПолучитьНастройки(Маршрут);
	Если ПараметрыRMQ = Неопределено Тогда
		ВызватьИсключение "Не заданы параметры подключения "+Маршрут;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	CRM_ОчередьRMQ.ID КАК ID,
		|	CRM_ОчередьRMQ.Сообщение КАК Сообщение,
		|	CRM_ОчередьRMQ.Маршрутизация КАК Маршрутизация
		|ИЗ
		|	РегистрСведений.CRM_ОчередьRMQ КАК CRM_ОчередьRMQ";	
	РезультатЗапроса = Запрос.Выполнить(); 
	Если НЕ РезультатЗапроса.Пустой() Тогда
		КлиентКомпоненты = ПолучитьКомпонентуСервер();
		КлиентКомпоненты.Connect(ПараметрыRMQ.Адрес,ПараметрыRMQ.Порт,ПараметрыRMQ.Логин,ПараметрыRMQ.Пароль,ПараметрыRMQ.ВиртуальныйХост);
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл 			
			Попытка							
				КлиентКомпоненты.BasicPublish(ПараметрыRMQ.ТочкаОбмена,Выборка.Маршрутизация,Выборка.Сообщение,1,Ложь); 
				
				//очистка очереди из 1С
				МЗ = РегистрыСведений.CRM_ОчередьRMQ.СоздатьМенеджерЗаписи();
				МЗ.ID = Выборка.ID; 
				МЗ.Удалить();
				
			Исключение
				СистемнаяОшибка = ОписаниеОшибки();
				ТекстСообщения = "Ошибка отправки сообщения!%СистемнаяОшибка%";
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СистемнаяОшибка%", СистемнаяОшибка);
				ВызватьИсключение ТекстСообщения;
			КонецПопытки;
					
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура РегламентноеЗадание_ПрочитатьRMQ() Экспорт
	//1. Остатки
	Маршрут = "CRM_remaining";  
	ПараметрыRMQ = РегистрыСведений.CRM_ПараметрыПодключенияRMQ.ПолучитьНастройки(Маршрут);
	Если ПараметрыRMQ = Неопределено Тогда
		ВызватьИсключение "Не заданы параметры подключения "+Маршрут;
	КонецЕсли;;
	
	Попытка
		КлиентКомпоненты = ПолучитьКомпонентуСервер();
		КлиентКомпоненты.Connect(ПараметрыRMQ.Адрес,ПараметрыRMQ.Порт,ПараметрыRMQ.Логин,ПараметрыRMQ.Пароль,ПараметрыRMQ.ВиртуальныйХост);	
		КлиентКомпоненты.DeclareQueue(ПараметрыRMQ.ИмяОчереди, Истина, Истина, Ложь, Ложь);
		
		Потребитель = КлиентКомпоненты.BasicConsume(ПараметрыRMQ.ИмяОчереди, "", Истина, Ложь, 0);
		
		ОтветноеСообщение = "";
		Пока КлиентКомпоненты.BasicConsumeMessage("", ОтветноеСообщение, 5) Цикл		
			ОбновитьОстатокНоменклатуры(ОтветноеСообщение);			
			КлиентКомпоненты.BasicAck();
		КонецЦикла;
		
		КлиентКомпоненты.BasicCancel("");
	Исключение
		ВызватьИсключение КлиентКомпоненты.GetLastError();
	КонецПопытки;
	
	
	//2. статусы заказов
	Маршрут = "CRM_OrderStatus";  
	ПараметрыRMQ = РегистрыСведений.CRM_ПараметрыПодключенияRMQ.ПолучитьНастройки(Маршрут);
	Если ПараметрыRMQ = Неопределено Тогда
		ВызватьИсключение "Не заданы параметры подключения "+Маршрут;
	КонецЕсли;;
	
	Попытка
		КлиентКомпоненты = ПолучитьКомпонентуСервер();
		КлиентКомпоненты.Connect(ПараметрыRMQ.Адрес,ПараметрыRMQ.Порт,ПараметрыRMQ.Логин,ПараметрыRMQ.Пароль,ПараметрыRMQ.ВиртуальныйХост);	
		КлиентКомпоненты.DeclareQueue(ПараметрыRMQ.ИмяОчереди, Истина, Истина, Ложь, Ложь);
		
		Потребитель = КлиентКомпоненты.BasicConsume(ПараметрыRMQ.ИмяОчереди, "", Истина, Ложь, 0);
		
		ОтветноеСообщение = "";
		Пока КлиентКомпоненты.BasicConsumeMessage("", ОтветноеСообщение, 5) Цикл		
			ОбновитьСтатусЗаказа(ОтветноеСообщение);			
			КлиентКомпоненты.BasicAck();
		КонецЦикла;
		
		КлиентКомпоненты.BasicCancel("");
	Исключение
		ВызватьИсключение КлиентКомпоненты.GetLastError();
	КонецПопытки;
КонецПроцедуры

Процедура ПриЗаписиRMQПриЗаписи(Источник, Отказ) Экспорт
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	//Тут дополнительные условия на отправку.
	//В моем случае разрешено всегда, без фильтров
	ОтправкаРазрешена = Истина;	
	Если не ОтправкаРазрешена Тогда
		Возврат;
	КонецЕсли;
	
	//Получение настройки маршрутизации
	//В момем случае в настройки не выношу 
	Маршрутизация = "wms.1"; //лучше в настройки
	
	//Формирование сообщения json
	Данные = Новый Структура;  
    Данные.Вставить("Ссылка", XMLСтрока(Источник.Ссылка)); 
	Данные.Вставить("Наименование", Источник.Наименование);
	Данные.Вставить("Код", Источник.Код);	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку();
    ЗаписатьJSON(ЗаписьJSON,Данные);  
    Сообщение = ЗаписьJSON.Закрыть();
	
	//Запись сообщения в очередь 
	МЗ = РегистрыСведений.ОчередьRMQ.СоздатьМенеджерЗаписи();
	МЗ.ID = новый УникальныйИдентификатор;
	МЗ.Сообщение = Сообщение;
	МЗ.Маршрутизация = Маршрутизация;
	МЗ.Записать();
	
КонецПроцедуры  

Процедура ОбновитьОстатокНоменклатуры(СтрокаJSON)
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);  
	Данные = ПрочитатьJSON(ЧтениеJSON);            
	ЧтениеJSON.Закрыть();
	
	НоменклатураСсылка = Справочники.CRM_Номенклатура.НайтиПоКоду(Данные.КодCRM);
	Если ЗначениеЗаполнено(НоменклатураСсылка) Тогда
		НоменклатураОбъект = НоменклатураСсылка.ПолучитьОбъект();		
	    НоменклатураОбъект.СвободныйОстаток = Данные.СвободныйОстаток;
		НоменклатураОбъект.ОбменДанными.Загрузка = Истина;
		НоменклатураОбъект.Записать();
	КонецЕсли; 
		
КонецПроцедуры

Процедура ОбновитьСтатусЗаказа(СтрокаJSON)
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);  
	Данные = ПрочитатьJSON(ЧтениеJSON);            
	ЧтениеJSON.Закрыть();
	
	НужныйСтатус = Неопределено;
	Если Данные.Статус = "Собран" Тогда  
		НужныйСтатус = Перечисления.CRM_СтатусыЗаказов.Собран;
	ИначеЕсли Данные.Статус = "Новый" Тогда  
		НужныйСтатус = Перечисления.CRM_СтатусыЗаказов.ВСборке;
	КонецЕсли;
	Если НужныйСтатус <> Неопределено Тогда	
		ДокСсылка = Документы.CRM_Заказ.НайтиПоНомеру(Данные.НомерCRM);
		Если ЗначениеЗаполнено(ДокСсылка) Тогда
			ДокОбъект = ДокСсылка.ПолучитьОбъект();		
		    ДокОбъект.Статус = НужныйСтатус;
			ДокОбъект.ОбменДанными.Загрузка = Истина;
			ДокОбъект.Записать();
		КонецЕсли;	
	КонецЕсли;
	
	 
		
КонецПроцедуры

Функция ПолучитьКомпонентуСервер()
	
	КлиентКомпоненты = Неопределено;
	Если Не ИнициализироватьКомпонентуКлиентСервер(КлиентКомпоненты) Тогда
		
		ПодключитьКомпонентуСервер();
		ИнициализироватьКомпонентуКлиентСервер(КлиентКомпоненты);
		
	КонецЕсли;
	
	Возврат КлиентКомпоненты;
КонецФункции  

Процедура ПодключитьКомпонентуСервер(КомпонентаПодключена = Неопределено)
	
	
	МакетВнешнейКомпоненты    = ПолучитьОбщийМакет("КомпонентаRMQ");
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(МакетВнешнейКомпоненты, Новый УникальныйИдентификатор());
	
	КомпонентаПодключена = ПодключитьВнешнююКомпоненту(
			АдресВоВременномХранилище,
			"BITERP",
			ТипВнешнейКомпоненты.Native);
	//Сообщить(НСтр("ru = 'Компонента подключена!'"));
КонецПроцедуры

Функция ИнициализироватьКомпонентуКлиентСервер(Компонента)
	
	Попытка
		Компонента  = Новый("AddIn.BITERP.PinkRabbitMQ");
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции




